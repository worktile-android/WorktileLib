import org.codehaus.groovy.runtime.GStringImpl
import org.codehaus.groovy.runtime.MethodClosure

apply plugin: 'maven-publish'

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.10'
    repositories {
        maven { url 'https://maven.worktile-android.com/repository/jcenter-proxy' }
        maven { url 'https://maven.worktile-android.com/repository/maven-central' }
        maven { url 'https://maven.worktile-android.com/repository/google-proxy' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.30"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven { url 'https://maven.worktile-android.com/repository/jcenter-proxy' }
        maven { url 'https://maven.worktile-android.com/repository/maven-central' }
        maven { url 'https://maven.worktile-android.com/repository/worktile' }
        maven { url 'https://maven.worktile-android.com/repository/debug' }
        maven { url 'https://maven.worktile-android.com/repository/google-proxy' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def configPublish(Project project, group, _artifact, ver) {

    project.afterEvaluate {
        project.task([type: Jar], "sourceJar") {
            from project.extensions["android"].sourceSets.main.java.srcDirs
        }

        project.publishing {
            publications {
                // Creates a Maven publication called "release".
                release(MavenPublication) {
                    // Applies the component for the release build variant.
                    from project.components.release

                    artifact project.sourceJar {
                        classifier "sources"
                    }

                    // You can then customize attributes of the publication as shown below.
                    groupId = group
                    artifactId = _artifact
                    version = ver
                }

                debug(MavenPublication) {
                    // Applies the component for the release build variant.
                    from project.components.debug

                    artifact project.sourceJar {
                        classifier "sources"
                    }

                    // You can then customize attributes of the publication as shown below.
                    groupId = group
                    artifactId = _artifact
                    version = ver
                }
            }

            repositories {
                maven {
                    name "worktile"
                    url "https://maven.worktile-android.com/repository/worktile/"
                    credentials {
                        it.username = MAVEN_USERNAME
                        it.password = MAVEN_PASSWORD
                    }
                }

                maven {
                    name "debug"
                    url "https://maven.worktile-android.com/repository/debug/"
                    credentials {
                        it.username = MAVEN_USERNAME
                        it.password = MAVEN_PASSWORD
                    }
                }
            }
        }
    }
}

ext {
    common_version = "0.0.9.220050821-SNAPSHOT"
    widgets_version = "0.0.1.22040815-SNAPSHOT"
    recyclerview_version = "0.9.7.21101500-SNAPSHOT"
    recyclerview2_version = "1.0.0.2206280201-SNAPSHOT"
    json_version = "0.2.1.22031623-SNAPSHOT"
    kanban_version = "0.0.1.22012702-SNAPSHOT"
    wt_common = "com.worktile:common:$common_version" as GStringImpl
    wt_widgets = "com.worktile.ui:widgets:$widgets_version" as GStringImpl
    wt_recyclerview = "com.worktile.ui:recyclerview:$recyclerview_version" as GStringImpl
    wt_recyclerview2 = "com.worktile.ui:recyclerview2:$recyclerview2_version" as GStringImpl
    wt_kanban = "com.worktile.ui:kanban:$kanban_version" as GStringImpl

    configPublish = this.&configPublish as MethodClosure
    kotlin_stdlib_jdk7 = "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version" as GStringImpl
    kotlin_reflect = "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version" as GStringImpl
    kotlinx_coroutines_core = "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    appcompact = "androidx.appcompat:appcompat:1.2.0"
    core_ktx = "androidx.core:core-ktx:1.3.1"
    acticity_ktx = "androidx.activity:activity-ktx:1.1.0"
    lifecycle_runtime_ktx = "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-rc01"
    recyclerview = 'androidx.recyclerview:recyclerview:1.2.1'
    constraintlayout = "androidx.constraintlayout:constraintlayout:2.0.1"
    viewpager2 = "androidx.viewpager2:viewpager2:1.0.0"
}
