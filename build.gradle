import org.codehaus.groovy.runtime.GStringImpl
import org.codehaus.groovy.runtime.MethodClosure

apply plugin: 'maven-publish'

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.10'
    repositories {
        maven { url 'https://maven.mokimatrix.com/repository/jcenter-proxy' }
        maven { url 'https://maven.mokimatrix.com/repository/maven-central' }
        maven { url 'https://maven.mokimatrix.com/repository/google-proxy' }
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven { url 'https://maven.mokimatrix.com/repository/jcenter-proxy' }
        maven { url 'https://maven.mokimatrix.com/repository/maven-central' }
        maven { url 'https://maven.mokimatrix.com/repository/worktile' }
        maven { url 'https://maven.mokimatrix.com/repository/google-proxy' }
        google()
        jcenter()

    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def configPublish(Project project, group, artifact, ver) {
    project.afterEvaluate {
        project.publishing {
            publications {
                // Creates a Maven publication called "release".
                release(MavenPublication) {
                    // Applies the component for the release build variant.
                    from project.components.release

                    // You can then customize attributes of the publication as shown below.
                    groupId = group
                    artifactId = artifact
                    version = ver
                }

                debug(MavenPublication) {
                    // Applies the component for the release build variant.
                    from project.components.debug

                    // You can then customize attributes of the publication as shown below.
                    groupId = group
                    artifactId = artifact
                    version = ver
                }
            }

            repositories {
                maven {
                    name "worktile"
                    url "https://maven.mokimatrix.com/repository/worktile/"
                    credentials {
                        it.username = MAVEN_USERNAME
                        it.password = MAVEN_PASSWORD
                    }
                }

                maven {
                    name "debug"
                    url "https://maven.mokimatrix.com/repository/debug/"
                    credentials {
                        it.username = MAVEN_USERNAME
                        it.password = MAVEN_PASSWORD
                    }
                }
            }
        }
    }
}

ext {
    common_version = "0.0.4"
    widgets_version = "0.0.1"
    recyclerview_version = "0.9.5-SNAPSHOT"
    wt_common = "com.worktile:common:$common_version" as GStringImpl
    wt_widgets = "com.worktile.ui:widgets:$widgets_version" as GStringImpl
    wt_recyclerview = "com.worktile.ui:recyclerview:$recyclerview_version" as GStringImpl

    configPublish = this.&configPublish as MethodClosure
    kotlin_stdlib_jdk7 = "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version" as GStringImpl
    kotlin_reflect = "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version" as GStringImpl
    kotlinx_coroutines_core = "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9"
    appcompact = "androidx.appcompat:appcompat:1.2.0"
    core_ktx = "androidx.core:core-ktx:1.3.1"
    acticity_ktx = "androidx.activity:activity-ktx:1.1.0"
    lifecycle_runtime_ktx = "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
    recyclerview = 'androidx.recyclerview:recyclerview:1.1.0'
    constraintlayout = "androidx.constraintlayout:constraintlayout:2.0.1"
    viewpager2 = "androidx.viewpager2:viewpager2:1.0.0"
}
